plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.github.getplusm'
version = '2.4.9'
description = 'PLAZMER-ENGINE'

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    java.sourceCompatibility = JavaVersion.VERSION_17
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    repositories {
        mavenLocal()
        mavenCentral()

        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        maven { url = "https://oss.sonatype.org/content/groups/public/" }
        maven { url = 'https://jitpack.io' }

        // floadgate api
        maven { url = 'https://repo.opencollab.dev/main/' }
        // WorldGuard
        maven { url = 'https://maven.enginehub.org/repo/' }
        // PAPI
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    }
}

dependencies {
    compileOnly 'com.zaxxer:HikariCP:5.0.1'

    shadow 'com.github.caseyscarborough:pastebin-api-java:0.1.3'
    shadow project(path: ':engine', configuration: 'default')
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.runtimeClasspath, project.configurations.shadow]
    archiveFileName.set(rootProject.description + '-' + rootProject.version + '.jar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                classifier = null
            }
            groupId = group
            artifactId = description
            version = project.version

            pom {
                name.set(description)
                description.set("Plugin Engine")
                url.set("https://github.com/getplusm/Engine")
            }
        }
    }
    repositories {
        maven {
            name = 'jitpack'
            url = 'https://jitpack.io'
        }
    }
}