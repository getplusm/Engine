plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
group = 'com.github.getplusm'
version = '2.4.8'
description = 'PLAZMER-ENGINE'

java.sourceCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenLocal()
        mavenCentral()
        // Spigot API * start
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            // As of Gradle 5.1, you can limit this to only those
            // dependencies you expect from it
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven { url = 'https://repo.dmulloy2.net/repository/public/' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://oss.sonatype.org/content/repositories/central' }

        // Spigot API * end

        // more & more
        maven { url = 'https://jitpack.io' }

        // floadgate api
        maven { url = 'https://repo.opencollab.dev/maven-snapshots/' }

        // PlasmoVoice
        maven { url = 'https://repo.plo.su/' }

        // WorldGuard
        maven { url = 'https://maven.enginehub.org/repo/' }
        // PAPI
        maven {
            url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        }
    }
}

dependencies {
    compileOnly 'org.jooq:jooq:3.19.11'
    compileOnly 'org.jooq:jooq-meta:3.19.11'
    compileOnly 'org.jooq:jooq-codegen:3.19.11'
    compileOnly 'mysql:mysql-connector-java:8.0.33'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'

    shadow project(path: ':engine', configuration: 'default')
    shadow project(path: ':folia', configuration: 'default')
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.runtimeClasspath, project.configurations.shadow]
    archiveFileName.set(rootProject.description + '-' + rootProject.version + '.jar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                classifier = null
            }
            groupId = "com.github.getplusm"
            artifactId = "PLAZMER-ENGINE"
            version = project.version

            pom {
                name.set("PLAZMER-ENGINE")
                description.set("Plugin Engine")
                url.set("https://github.com/getplusm/Engine")
            }
        }
    }
    repositories {
        maven {
            name = 'jitpack'
            url = 'https://jitpack.io'
        }
    }
}
